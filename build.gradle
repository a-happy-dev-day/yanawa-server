plugins {
    id 'org.springframework.boot' version '2.6.9'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.asciidoctor.jvm.convert' version "3.3.2"
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.4.0.2513"
}

configurations {
    asciidoctorExt
}

group = 'fashionable.simba'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
    set('springCloudVersion', "2021.0.3")
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.json', name: 'json', version: '20220320'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    // https://mvnrepository.com/artifact/nl.jqno.equalsverifier/equalsverifier
    testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.10.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:4.2.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured:2.0.5.RELEASE'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "a-happy-dev-day_yanawa-server"
        property "sonar.organization", "a-happy-dev-day"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.exclusions", "src/main/java/fashionable/simba/yanawaserver/matching/application/dto/RecruitmentRequsest.java, " +
                "src/main/java/fashionable/simba/yanawaserver/matching/application/dto/RecruitmentResponse.java"
    }
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacoco {
    toolVersion = "0.8.7"
    reportsDirectory = layout.buildDirectory.dir('jacocoDir')
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            // 클래스 단위로 커버리지 체크
            element = 'CLASS'

            // 라인 커버리지 제한을 90%로 설정
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }

            // 브랜치 커버리지 제한을 90%로 설정
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }

            excludes = ["*.YanawaserverApplication", "*.HealthController", "*.*Request*", "*.*Response*"]
        }
    }
}


ext {
    snippetsDir = file('build/generated-snippets')
}

task testDocument(type: Test) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "*.documentation.*"
    }
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}
